%code requires {
    #include <stdio.h>
    #include <stdlib.h>
    #include <string>
    #include <stdbool.h>
    #include "shared_ptrs_nodes/Expressions.h"
	#include "shared_ptrs_nodes/Statements.h"
	#include "shared_ptrs_nodes/Classes.h"
	#include <iostream>

    #define YYERROR_VERBOSE 1   
    #define SAVE_LOCATION p->position.firstLine=yylloc.first_line;p->position.firstColumn=yylloc.first_column;p->position.lastLine=yylloc.last_line;p->position.lastColumn=yylloc.last_column;
    #define SET_LOCATION p->position.firstLine=newP->position.first_line;p->position.firstColumn=newP->position.first_column;p->position.lastLine=newP->position.last_line;p->position.lastColumn=newP->position.last_column;
}

%code {    
	int yylex (void);
	int yyerror ( CProgram **program, const char * );
	extern YYLTYPE yylloc;
	
}

%error-verbose // более подробная информация об ошибке
%verbose // output-файл с правилами грамматики
%parse-param { CProgram** program } // результат работы анализатора
%locations  // Подключаем yylloc без появления @n в коде

%union {

	CProgram* Program; // Указатель на класс-программу
	CClass* Class; // Указатель класс
	CClassList* ClassDeclarationList;
	IStatement* Statement;
	IExpression* Expression;
	CType* Type;
	CField* VarDeclaration;
	CFieldList* VarDeclarationList;
	CArgument* Argument;
	CArgumentList* ArgumentList;
	CArgumentList* NonEmptyArgumentList;
	CMethod* MethodDeclaration;
	CMethodList* MethodDeclarationList;
	CMainMethod* MainMethod;
	CMainClass* MainClass;
	CExpList* ExpressionList;
	CExpList* NonEmptyExpressionList;
	char *string;
	CIdExp* Identifier;
}

%token NEW CLASS EXTENDS THIS RETURN

%token PUBLIC PRIVATE STATIC

%token PRINTLN

%token INT BOOLEAN

%token VOID INT_ARRAY LENGTH

%token TRUE FALSE

%token WHILE IF ELSE

%token <string> NUM

%token <string> ID

%token LPAREN RPAREN

%token LBRACKET RBRACKET

%token LBRACE RBRACE

%token COMMA DOT SEMI

//%token COMMENT

%token AND OR

%token PLUS MINUS TIMES MOD

%token MAIN

%token STRING

%token LESS GREATER NOT

%token ASSIGN

%left LBRACKET DOT LPAREN
%right NOT
%left TIMES MOD
%left MINUS PLUS
%left LESS GREATER
%left AND
%left OR
%right ASSIGN

// Особые (типизированные) токены
%type <Program> Program
%type <Expression> Expression
%type <MainClass> MainClass
%type <Class> Class
%type <ClassDeclarationList> ClassDeclarationList
%type <Statement> Statement
%type <Statement> StatementList
%type <MainMethod> MainMethod
%type <MethodDeclaration> MethodDeclaration
%type <MethodDeclarationList> MethodDeclarationList
%type <VarDeclaration> VarDeclaration
%type <VarDeclarationList> VarDeclarationList
%type <Argument> Argument
%type <Type> Type
%type <ArgumentList> ArgumentList
%type <NonEmptyArgumentList> NonEmptyArgumentList
%type <ExpressionList> ExpressionList
%type <NonEmptyExpressionList> NonEmptyExpressionList
%type <Identifier> Identifier

%%

Program:
	MainClass ClassDeclarationList { 
		*program = $$ = new CProgram( $1, $2 );
	}
	;

MainClass:
	CLASS Identifier LBRACE
		MainMethod
	RBRACE
	{	
		$$ = new CMainClass( $2, $4 );
	}
	;

MainMethod:
	PUBLIC STATIC VOID MAIN LPAREN STRING LBRACKET RBRACKET Identifier RPAREN
	LBRACE
		VarDeclarationList
		StatementList
	RBRACE 
	{	
		$$ = new CMainMethod( 0, $9, (CFieldList *) $12,(CCompoundStm *)$13 );
	}
	;

ClassDeclarationList:
	%empty { 
		$$ = new CClassList();
	}
	|
	ClassDeclarationList Class { $$ = $1; $1->Add($2); }
	;

Class:
	CLASS Identifier LBRACE
		VarDeclarationList
		MethodDeclarationList
	RBRACE { 
		$$ = new CClass( $2, 0, $4, $5 );
	}
	| 
	CLASS Identifier EXTENDS Identifier LBRACE
		VarDeclarationList
		MethodDeclarationList
	RBRACE { 
		$$ = new CClass( $2, $4, $6, $7 );
	}
	;

MethodDeclaration:
	PUBLIC Type Identifier LPAREN ArgumentList RPAREN LBRACE
		VarDeclarationList
		StatementList
		RETURN Expression SEMI
	RBRACE
	//true means public
	{ 
		$$ = new CMethod( $2, $11, $3, $5, (CFieldList *)$8,(CCompoundStm *)$9, true );
	}
	|
	//false means private
	PRIVATE Type Identifier LPAREN ArgumentList RPAREN LBRACE
		VarDeclarationList
		StatementList
		RETURN Expression SEMI
	RBRACE
	{ 
		$$ = new CMethod( $2, $11, $3, $5, (CFieldList *)$8, (CCompoundStm *)$9, false );
	}
	;

MethodDeclarationList:
	%empty { 
		$$ = new CMethodList();
	}
	|
	MethodDeclarationList MethodDeclaration { 
		$$ = $1; $1->Add($2);
	}
	;

VarDeclaration:
	Type Identifier SEMI {
	    $2->isInstance = true;
		$$ = new CField( $1, $2 );
	}
	;

VarDeclarationList:
	%empty { 
		$$ = new CFieldList();
	}
	|
	VarDeclarationList VarDeclaration {
		$$ = $1; $1->Add( $2 );
	}
	;

Argument:
	Type Identifier {
	    $2->isInstance = true;
		$$ = new CArgument( $1, $2 );
	}
	;

Type:
	INT_ARRAY { 
		auto p = $$ = new CType( enums::TPrimitiveType::INT_ARRAY );
		SAVE_LOCATION
	}
	|
	BOOLEAN { 
	    auto p = $$ = new CType( enums::TPrimitiveType::BOOLEAN );
		SAVE_LOCATION
	}
	|
	INT { 
		auto p = $$ = new CType( enums::TPrimitiveType::INT );
		SAVE_LOCATION
	}
	|
	Identifier { $$ = new CType( $1 ); }
	;
NonEmptyArgumentList:
	Argument { $$ = new CArgumentList($1); }
	|
	NonEmptyArgumentList COMMA Argument { $$ = $1; $1->Add($3); }
	;

ArgumentList:
	%empty { $$ = new CArgumentList(); }
	|
	NonEmptyArgumentList { $$ = $1; }
	;

StatementList:
	%empty {$$ = new CCompoundStm( 0, 0 ); }
	|
	Statement StatementList { $$ = new CCompoundStm($1, $2);  }
	;

Statement:
	LBRACE StatementList RBRACE { $$ = $2; }
	| 
	IF LPAREN Expression RPAREN Statement ELSE Statement { $$ = new CIfStm( $3, $5, $7 );  }
	| 
	WHILE LPAREN Expression RPAREN Statement { $$ = new CWhileStm( $3, $5 );  }
	| 
	PRINTLN LPAREN Expression RPAREN SEMI { $$ = new CPrintStm( $3 ); }
	|
	Identifier ASSIGN Expression SEMI { $1->isInstance = true; $$ = new CAssignStm( $1, $3 );  }
	|
	Identifier LBRACKET Expression RBRACKET ASSIGN Expression SEMI { $1->isInstance = true; $$ = new CAssignSubscriptStm( $1, $3, $6 );  }
	;

Expression:
	Expression PLUS Expression { $$ = new COpExp( $1, $3, enums::TOperation::PLUS );  }
	|
	Expression MINUS Expression { $$ = new COpExp( $1, $3, enums::TOperation::MINUS );  }
	|
	Expression TIMES Expression { $$ = new COpExp( $1, $3, enums::TOperation::MULTIPLY );  }
	|
	Expression AND Expression { $$ = new CLogOpExp( $1, $3, enums::TLogicalOperation::AND );  }
	|
	Expression OR Expression { $$ = new CLogOpExp( $1, $3, enums::TLogicalOperation::OR );  }
	|
	Expression LESS Expression { $$ = new CCompExp( $1, $3, enums::TCompareOperation::LESS );  }
	|
	Expression GREATER Expression { $$ = new CCompExp( $1, $3, enums::TCompareOperation::GREATER );  }
	|
	Expression MOD Expression { $$ = new COpExp( $1, $3, enums::TOperation::MOD );  }
	|
	Expression LBRACKET Expression RBRACKET { $$ = new CByIndexExpression($1, $3);  }
	|
	Expression DOT LENGTH { $$ = new CGetLengthExp( $1 );  }
	|
	Expression DOT Identifier LPAREN ExpressionList RPAREN { $$ = new CCallMethodExp( $1, $3, (CExpList*) $5) ;  }
	|
	Expression DOT Identifier { $3->isInstance = true; $$ = new CGetFieldByThisExpression( $3 ); }
	|
	MINUS Expression { $$ = new CUnarMinusExp( $2 ); }
	|
	NUM { auto p = $$ = new CNumExp($1); SAVE_LOCATION }
	|
	TRUE { auto p = $$ = new CLogExp(true); SAVE_LOCATION }
	|
	FALSE { auto p = $$ = new CLogExp(false); SAVE_LOCATION }
	|
	Identifier { $1->isInstance = true; $$ = $1; }
	|
	NEW INT LBRACKET Expression RBRACKET { $$ = new CNewArrayExpression($4);  }
	|
	NEW Identifier LPAREN  RPAREN { $$ = new CNewIdentifier( $2 );  }
	|
	LPAREN Expression RPAREN { $$ = $2; }
	|
	THIS { auto p = $$ = new CThisExpression(); SAVE_LOCATION }
	|
	NOT Expression { $$ = new CNegativeExpression($2);  }
	;

NonEmptyExpressionList:
	Expression { $$ = new CExpList($1);  }
	|
	NonEmptyExpressionList COMMA Expression { $$ = $1; $1->Add($3); }
	;

ExpressionList:
	%empty { $$ = new CExpList();  }
	|
	NonEmptyExpressionList { $$ = $1; }
	;

Identifier:
	ID { auto p = $$ = new CIdExp( $1 ); SAVE_LOCATION }
	;

%%

int yyerror(std::string s) {
  return -1;
}

int yyerror(CProgram **program, const char *s) {
	std::cout << s << "\n";
	std::cout << "Something went wrong\n";
	return yyerror(std::string(s));
}